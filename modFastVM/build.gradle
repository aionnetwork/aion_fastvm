ext.moduleName = 'aion.fastvm'

/* 
 * This Gradle project is meant to be called as a subproject by
 * the aion Gradle project and not intended to be built on its own.
 *
 * See: https://github.com/aionnetwork/aion
 */

repositories {
    maven { url "https://oss.sonatype.org/content/groups/staging" }

    flatDir {
        dirs './lib'
    }
}

test.dependsOn copyNativeLibsForModuleTests

targetCompatibility = 10
sourceCompatibility = 10

def getCommitHash = { ->
    def hashStdOut = new ByteArrayOutputStream()
    exec {
        commandLine "sh", "-c", "git log --pretty=format:%h | head -1"
        standardOutput = hashStdOut
    }

    return hashStdOut.toString().trim()
}


sourceSets {

    jar.baseName = 'aion_fastvm-' + getCommitHash()

    test {
        resources {
            srcDirs = ['test']
        }
    }
}

dependencies {
    compile 'org.json:json:20180813'
    compile files("${rootProject.projectDir}/lib/libJson.jar")
    compile files("${rootProject.projectDir}/lib/libnsc.jar")
    compile files("${rootProject.projectDir}/lib/aion-types-d6eb8f7.jar")
    compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.0'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
    compile 'org.slf4j:slf4j-api:1.7.25'

    testCompile 'junit:junit:4.12'
    testCompile 'com.google.truth:truth:0.42'
    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile "org.mockito:mockito-core:2.23.0"
}

// Skip unit tests when doing build task; unit tests are all mixed up with 
// long-running integration tests in this module,so doing this to avoid 
// build taking too long.
build.dependsOn.remove("check")

